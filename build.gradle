buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
		classpath 'org.ajoberstar:gradle-git:0.2.0'
	}
}

allprojects {
	version = '1.2-SNAPSHOT'
	group = 'co.freeside'

	ext {
		groovyVersion = '1.8.8'
		httpClientVersion = '4.2.2'
		junitVersion = '4.10'
		spockVersion = '0.7-groovy-1.8'
		jettyVersion = '7.3.1.v20110307'
		nettyVersion = '4.0.4.Final'

		groovyDependency = "org.codehaus.groovy:groovy-all:$groovyVersion"
		httpClientDependency = "org.apache.httpcomponents:httpclient:$httpClientVersion"
		junitDependency = "junit:junit:$junitVersion"
		spockDependency = "org.spockframework:spock-core:$spockVersion"
		jettyDependency = "org.eclipse.jetty:jetty-server:$jettyVersion"
		// TODO: not sure we need netty-all
		nettyDependency = "io.netty:netty-all:$nettyVersion"

		publishedModules = [':betamax-core', ':betamax-proxy', ':betamax-httpclient', ':betamax-jetty', ':betamax-netty']
	}
}

subprojects {
	apply plugin: 'groovy'
	apply plugin: 'codenarc'

	repositories {
		mavenCentral()
	}

	dependencies {
		compile groovyDependency
		compile "com.google.guava:guava:14.0.1"

		testCompile(spockDependency) {
			exclude module: 'groovy-all'
			exclude module: 'junit-dep'
		}
	}

	codenarc {
		toolVersion = '0.17'
		ignoreFailures = true
	}

	test {
		testLogging {
			quiet {
				events 'failed'
				exceptionFormat 'short'
			}
		}
	}

	if (path in publishedModules) {
		apply plugin: 'nexus'

		modifyPom {
			dependencies.removeAll(dependencies.findAll { it.scope == 'test' })

			project {
				packaging 'jar'
				url 'http://freeside.co/betamax'
				inceptionYear '2011'

				scm {
					url 'scm:git@github.com:robfletcher/betamax.git'
					connection 'scm:git@github.com:robfletcher/betamax.git'
					developerConnection 'scm:git@github.com:robfletcher/betamax.git'
				}

				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}

				developers {
					developer {
						id 'robfletcher'
						name 'Rob Fletcher'
						url 'http://freeside.co/'
						roles {
							role 'Lead'
							role 'Founder'
						}
					}
				}
			}
		}

		nexus {
			sign = true
		}
	}
}

apply plugin: 'github-pages'

//archivesBaseName = 'betamax'

githubPages {
	repoUri = 'git@github.com:robfletcher/betamax.git'
	workingPath = "$buildDir/docs"
	pages {
		exclude '**/_site/**'
		from('src/docs') {
			exclude '**/*.less'
		}
	}
}
