buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
		classpath 'org.ajoberstar:gradle-git:0.2.0'
	}
}

allprojects {
	version = '1.2-SNAPSHOT'
	group = 'co.freeside'

	ext {
		groovyVersion = '1.7.8'
		httpClientVersion = '4.2.1'
		spockVersion = '0.6-groovy-1.7'

		groovyDependency = "org.codehaus.groovy:groovy-all:$groovyVersion"
		httpClientDependency = "org.apache.httpcomponents:httpclient:$httpClientVersion"
		spockDependency = "org.spockframework:spock-core:$spockVersion"
	}
}

subprojects {
	apply plugin: 'groovy'
	apply plugin: 'codenarc'
	apply plugin: 'nexus'

	repositories {
		mavenCentral()
	}

	modifyPom {
		dependencies.removeAll(dependencies.findAll { it.scope == 'test' })

		project {
			packaging 'jar'
			url 'http://freeside.co/betamax'
			inceptionYear '2011'

			scm {
				url 'scm:git@github.com:robfletcher/betamax.git'
				connection 'scm:git@github.com:robfletcher/betamax.git'
				developerConnection 'scm:git@github.com:robfletcher/betamax.git'
			}

			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution 'repo'
				}
			}

			developers {
				developer {
					id 'robfletcher'
					name 'Rob Fletcher'
					url 'http://freeside.co/'
					roles {
						role 'Lead'
						role 'Founder'
					}
				}
			}
		}
	}

	nexus {
		sign = true
	}

	codenarc {
		toolVersion = '0.17'
		ignoreFailures = true
	}

	test {
		testLogging {
			quiet {
				events 'failed'
				exceptionFormat 'short'
			}
		}
	}
}

apply plugin: 'github-pages'

//archivesBaseName = 'betamax'

githubPages {
	repoUri = 'git@github.com:robfletcher/betamax.git'
	workingPath = "$buildDir/docs"
	pages {
		exclude '**/_site/**'
		from('src/docs') {
			exclude '**/*.less'
		}
	}
}
