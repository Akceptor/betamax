/*
 * Copyright 2016 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
    pomModifications = []
    modifyPom = { pomModifications << it }
}

tasks.withType(Upload) {
    repositories.withType(org.gradle.api.artifacts.maven.MavenResolver) {
        it.pom.whenConfigured { pom ->
            pomModifications.each {
                configure(pom, it)
            }
        }
    }
}

modifyPom {
    dependencies.removeAll { it.scope == "test" }
}

modifyPom {
    project {
        name project.name
        description project.description
        url "https://github.com/betamaxteam/betamax"
        inceptionYear "2011"
        licenses {
            license([:]) {
                name "The Apache Software License, Version 2.0"
                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution "repo"
            }
        }
        scm {
            url "https://github.com/betamaxteam/betamax"
            connection "https://github.com/betamaxteam/betamax.git"
            developerConnection "scm:git@github.com:betamaxteam/betamax.git"
        }
        developers {
            developer {
                id "cowboygneox"
                name "Sean Freitag"
                roles {
                    role "Developer"
                    role "Lead"
                }
            }
            developer {
                id "robfletcher"
                name "Rob Fletcher"
                url "http://freeside.co/"
                roles {
                    role "Founder"
                    role "Developer"
                }
            }
        }
    }
}